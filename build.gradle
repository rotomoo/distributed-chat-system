buildscript {
    ext {
        springBootVersion = '3.2.1'
        managementVersion = '1.1.6'
        springCloudVersion = "2023.0.0"
        protobufVersion = "0.9.4"
    }

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${managementVersion}")
        classpath("com.google.protobuf:protobuf-gradle-plugin:${protobufVersion}")
    }
}


subprojects {

    // 디렉터리 제외
    if (!project.name.contains("distributed-chat-system")) {
        return
    }

    println("project name = " + project.name);

    apply plugin: "java"
    apply plugin: 'org.springframework.boot'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'com.google.protobuf'

    group = 'com'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // web
        implementation 'org.springframework.boot:spring-boot-starter-web'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // protobuf
        implementation 'com.google.protobuf:protobuf-java:3.25.2'

        // validation
        implementation 'org.springframework.boot:spring-boot-starter-validation'
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:${property("springCloudVersion")}")
        }
    }

    test {
        useJUnitPlatform()
        systemProperty 'jasypt.encryptor.password', System.getenv("jasypt_encryptor_password")
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:3.6.1"
        }
    }
}